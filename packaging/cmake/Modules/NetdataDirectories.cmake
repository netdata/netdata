# Handling of install directories.
#
# Copyright (c) 2024 Netdata Inc
# SPDX-License-Identifier: GPL

include(GNUInstallDirs)

set(NETDATA_OBJECT_LIB_DIR "${CMAKE_INSTALL_LIBDIR}/${PROJECT_NAME}")
string(REGEX REPLACE "/${CMAKE_LIBRARY_ARCHITECTURE}/" "/" _nd_tmp_lib_dir "${NETDATA_OBJECT_LIB_DIR}")
string(REGEX REPLACE "lib64/" "lib/" NETDATA_NOARCH_LIB_DIR "${_nd_tmp_lib_dir}")
unset(_nd_tmp_lib_dir)

set(NETDATA_USER_CONFIG_DIR "${CMAKE_INSTALL_SYSCONFDIR}/${PROJECT_NAME}")
set(NETDATA_STOCK_CONFIG_DIR "${NETDATA_NOARCH_LIB_DIR}/conf.d")
set(NETDATA_SYSTEM_FILE_DIR "${NETDATA_NOARCH_LIB_DIR}/system")
set(NETDATA_LIBEXEC_DIR "${CMAKE_INSTALL_LIBEXECDIR}/${PROJECT_NAME}")
set(NETDATA_PLUGINS_DIR "${NETDATA_LIBEXEC_DIR}/plugins.d")
set(NETDATA_LOCALSTATE_DIR "${CMAKE_INSTALL_LOCALSTATEDIR}/lib/${PROJECT_NAME}")
set(NETDATA_CACHE_DIR "${CMAKE_INSTALL_LOCALSTATEDIR}/cache/${PROJECT_NAME}")
set(NETDATA_LOG_DIR "${CMAKE_INSTALL_LOCALSTATEDIR}/log/${PROJECT_NAME}")

if(NOT CMAKE_INSTALL_PREFIX STREQUAL "")
        string(REGEX REPLACE "/$" "" CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}")
endif()

set(CACHE_DIR "${CMAKE_INSTALL_PREFIX}/${NETDATA_CACHE_DIR}")
set(CONFIG_DIR "${CMAKE_INSTALL_PREFIX}/${NETDATA_USER_CONFIG_DIR}")
set(LIBCONFIG_DIR "${CMAKE_INSTALL_PREFIX}/${NETDATA_STOCK_CONFIG_DIR}")
set(LOG_DIR "${CMAKE_INSTALL_PREFIX}/${NETDATA_LOG_DIR}")
set(PLUGINS_DIR "${CMAKE_INSTALL_PREFIX}/${NETDATA_PLUGINS_DIR}")
set(VARLIB_DIR "${CMAKE_INSTALL_PREFIX}/${NETDATA_LOCALSTATE_DIR}")

# A non-default value is only used when building Debian packages (/${NETDATA_LOCALSTATE_DIR}/www)
if(NOT DEFINED WEB_DIR)
        set(WEB_DIR "${CMAKE_INSTALL_DATADIR}/${PROJECT_NAME}/web")
else()
        string(REGEX REPLACE "^/" "" WEB_DIR "${WEB_DIR}")
endif()
set(WEB_DEST "${WEB_DIR}")
set(WEB_DIR "${CMAKE_INSTALL_PREFIX}/${WEB_DEST}")

set(libsysdir_POST "${CMAKE_INSTALL_PREFIX}/${NETDATA_SYSTEM_FILE_DIR}")
set(pkglibexecdir_POST "${CMAKE_INSTALL_PREFIX}/${NETDATA_LIBEXEC_DIR}")
set(localstatedir_POST "${CMAKE_INSTALL_LOCALSTATEDIR_FULL}")
set(sbindir_POST "${CMAKE_INSTALL_SBINDIR_FULL}")
set(configdir_POST "${CONFIG_DIR}")
set(libconfigdir_POST "${LIBCONFIG_DIR}")
set(cachedir_POST "${CACHE_DIR}")
set(registrydir_POST "${VARLIB_DIR}/registry")
set(varlibdir_POST "${VARLIB_DIR}")
set(netdata_user_POST "${NETDATA_USER}")
